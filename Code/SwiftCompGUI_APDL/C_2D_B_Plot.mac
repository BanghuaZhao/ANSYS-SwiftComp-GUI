!******************************************************
! Create & Plot Beam Section                          *
!                                                     *
! written by: Banghua Zhao                            *
! Date: 2015                                          *
!                                                     *
! Modified by: Banghua Zhao                           *
! Date: 2016                                          *
!******************************************************


/NOPR

!*GET, Par, SECP, id, Item1, IT1NUM, Item2, IT2NUM

*get, t, secp, 1, type
*get, subt, secp, 1, SUBTYPE
*get, dd1,  secp, 1, data, 1 
*get, dd2,  secp, 1, data, 2 
*get, dd3,  secp, 1, data, 3 
*get, dd4,  secp, 1, data, 4 
*get, dd5,  secp, 1, data, 5 
*get, dd6,  secp, 1, data, 6 
*get, dd7,  secp, 1, data, 7 
*get, dd8,  secp, 1, data, 8 
*get, dd9,  secp, 1, data, 9 

! Rectanglar
*if, subt, eq, 'RECT', then
	! B, H, Nb, Nh
	RECTNG,-dd1/2,dd1/2,-dd2/2,dd2/2,    !x1,x2,y1,y2
*endif

! Quadrilateral
*if, subt, eq, 'QUAD', then
	! yI, zI, yJ, zJ, yK, zK, yL, zL
	k, ,dd1,dd2
	k, ,dd3,dd4
	k, ,dd5,dd6
	k, ,dd7,dd8
	L,1,2
	L,2,3
	L,3,4
	L,4,1
	AL,1,2,3,4
*endif

! Circular Solid
*if, subt, eq, 'CSOL', then
	! R
	PCIRC,dd1, ,0,360,
*endif

! Circular Tube
*if, subt, eq, 'CTUB', then
	! Ri,Ro
	PCIRC,dd2, dd1,0,360,
*endif

! Chanel
*if, subt, eq, 'CHAN', then
	! W1, W2, W3, t1, t2, t3
	! W1, W2 = Lengths of the flanges
	! W3 = Overall depth
	! t1, t2 = Flange thicknesses
	! t3 = Web thicknesses
	RECTNG, 0, dd1, 0, dd4
	RECTNG, 0, dd6, 0, dd3
	RECTNG, 0, dd2, dd3, dd3-dd5
	AADD,ALL
*endif

! I
*if, subt, eq, 'I', then
	! W1, W2, W3, t1, t2, t3
	! W1, W2 = Width of the bottom and top flanges
	! W3 = Overall depth 
	! t1, t2 = Flange thicknesses
	! t3 = Web thicknesses
	RECTNG, -dd1/2, dd1/2, 0, dd4
	RECTNG, -dd6/2, dd6/2, 0, dd3
	RECTNG, -dd2/2, dd2/2, dd3-dd5, dd3
	AADD,ALL
*endif

! Z
*if, subt, eq, 'Z', then
	! W1, W2, W3, t1, t2, t3
	! W1, W2 = Width of the bottom and top flanges
	! W3 = Overall depth 
	! t1, t2 = Flange thicknesses
	! t3 = Stem thicknesses
	RECTNG, -dd6/2, dd1-dd6/2, -dd4/2, dd4/2
	RECTNG, -dd6/2, dd6/2, -dd4/2, dd3-dd4/2
	RECTNG, -dd2+dd6/2,  dd6/2, dd3-dd4/2-dd5, dd3-dd4/2
	AADD,ALL
*endif

! L
*if, subt, eq, 'L', then
	! W1, W2, t1, t2
	! W1, W2 = Leg lengths
	! t1, t2 = Leg thicknesses
	RECTNG, -dd4/2, dd1-dd4/2, -dd3/2, dd3/2
	RECTNG, -dd4/2, dd3/2, -dd3/2, dd2-dd3/2
	AADD,ALL
*endif

! T
*if, subt, eq, 'T', then
	! W1, W2, t1, t2
	! W1 = Flange width
	! W2 = Overall depth
	! t1 = Flange thicknesses
	! t2 = Stem thicknesses
	RECTNG, -dd1/2, dd1/2, -dd3/2, dd3/2
	RECTNG, -dd4/2, dd4/2, -dd3/2, dd2-dd3/2
	AADD,ALL
*endif

! HATS
*if, subt, eq, 'HATS', then
	! W1, W2, W3, W4, t1, t2, t3, t4, t5
	! W1, W2 = Width of the brim
	! W3 = Width of the top of the hat
	! W4 = Overall depth
	! t1, t2 = Thickness of the brim
	! t3 = Thickness of the top of the hat
	! t4, t5 = Web thicknesses
	! dd5=t1  dd6=t2  dd7=t3  dd8=t4  dd9=t5
	RECTNG, 0, dd1, 0, dd5         ! t1   
	RECTNG, dd1+dd3-dd8-dd9,  dd1+dd3-dd8-dd9+dd2, 0, dd6  ! t2
	RECTNG, dd1-dd8, dd1+dd3-dd8, dd4-dd7, dd4  !t3
	RECTNG, dd1-dd8, dd1, 0, dd4  !t4
	RECTNG, dd1+dd3-dd8-dd9, dd1+dd3-dd8, 0, dd4 !t5
	AADD,ALL
*endif

! HREC
*if, subt, eq, 'HREC', then
	! W1, W2, t1, t2, t3, t4
	! W1 = Outer width of the box
	! W2 = Outer height of the box
	! t1, t2, t3, t4 = Wall thickness
	RECTNG, 0, dd1, 0, dd2, 
	RECTNG, dd3, dd1-dd4, dd5, dd2-dd6
	ASBA, 1, 2
*endif

/GOPR