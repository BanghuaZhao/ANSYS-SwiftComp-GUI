!*******************************************************
! This file defines the material properties            *
!                                                      *
! written by: Fang Jiang                               *
! Date: 2015                                           *
!                                                      *
! Modified by: Banghua Zhao                            *
! Date: 2016                                           *
!*******************************************************


/NOPR

*abcheck,16

ALLSEL,ALL 

*Get, nmate, MAT, 0, COUNT
 
*DO, I, 1, nmate
	*get,  E1,   EX, I
	*get, v12, PRXY, I
	*get, rho, DENS, I
	
	*IF, E1, NE, 7.888609052E-31, THEN   ! not anisotropic material 
		*get, E2, EY, I
		*get, E3, EZ, I
 	
	 	*IF, E2, EQ, 7.888609052E-31, AND, E3, EQ, 7.888609052E-31, THEN   ! isotropic material 
			isotropy_%I% = 0
			E1_%I% = E1
			v12_%I% = v12
			rho_%I%  = rho
			
			*ELSEIF, E2, NE, 7.888609052E-31, AND, E3, NE, 7.888609052E-31,  ! orthotropic material 	
				*get, G13,  GXZ, I
				*get, G12,  GXY, I
				*get, G23,  GYZ, I
				*get, v13, PRXZ, I
				*get, v23, PRYZ, I
						
				isotropy_%I% = 1
				E1_%I% = E1
				E2_%I% = E2
				E3_%I% = E3
				G12_%I% = G12
				G13_%I% = G13
				G23_%I% = G23
				v12_%I% = v12
				v13_%I% = v13
				v23_%I% = v23
				rho_%I% = rho
			
		*ENDIF
		
		*ELSEIF, E1, EQ, 7.888609052E-31            ! anisotropic material 
				isotropy_%I% = 2
				*GET, C11_%I%, ANEL, I, , , CONST, 1
				*GET, C12_%I%, ANEL, I, , , CONST, 2
				*GET, C13_%I%, ANEL, I, , , CONST, 3
				*GET, C16_%I%, ANEL, I, , , CONST, 4
				*GET, C14_%I%, ANEL, I, , , CONST, 5
				*GET, C15_%I%, ANEL, I, , , CONST, 6
				*GET, C22_%I%, ANEL, I, , , CONST, 7
				*GET, C23_%I%, ANEL, I, , , CONST, 8
				*GET, C26_%I%, ANEL, I, , , CONST, 9
				*GET, C24_%I%, ANEL, I, , , CONST, 10
				*GET, C25_%I%, ANEL, I, , , CONST, 11
				*GET, C33_%I%, ANEL, I, , , CONST, 12
				*GET, C36_%I%, ANEL, I, , , CONST, 13
				*GET, C34_%I%, ANEL, I, , , CONST, 14
				*GET, C35_%I%, ANEL, I, , , CONST, 15
				*GET, C66_%I%, ANEL, I, , , CONST, 16
				*GET, C64_%I%, ANEL, I, , , CONST, 17
				*GET, C65_%I%, ANEL, I, , , CONST, 18
				*GET, C44_%I%, ANEL, I, , , CONST, 19
				*GET, C45_%I%, ANEL, I, , , CONST, 20
				*GET, C55_%I%, ANEL, I, , , CONST, 21

				rho_%I% = rho
		
	*ENDIF
		
*ENDDO

w= 0

max_x1= NODE_X1(1)
min_x1= NODE_X1(1)
*Do, i, 2, nnode
	*if, NODE_X1(i), GT, max_x1, then
		max_x1= NODE_X1(i)
	*endif
	*if, NODE_X1(i), LT, min_x1, then
		min_x1= NODE_X1(i)
	*endif
*ENDDO
w1= max_x1-min_x1

max_x2 = node_x2(1)
min_x2 = node_x2(1)
*do, i, 2, nnode
	*if, node_x2(i), gt, max_x2, then
		max_x2 = node_x2(i)
	*endif
	*if, node_x2(i), lt, min_x2, then
		min_x2 = node_x2(i)
	*endif	
*enddo
w2= max_x2-min_x2

max_x3 = node_x3(1)
min_x3 = node_x3(1)
*do, i, 2, nnode
	*if, node_x3(i), gt, max_x3, then
		max_x3 = node_x3(i)
	*endif
	*if, node_x3(i), lt, min_x3, then
		min_x3 = node_x3(i)
	*endif	
*enddo
w3= max_x3-min_x3

! 1DSG
*IF, nSG, EQ, 1, then
	! Plate/Shell Model
	*if, Plate_flag, eq, 1, then
		w= 1
	*endif
	
	! Solid Model
	*if, Solid_flag, eq, 1, then
		w= C1DSG_thickness
	*endif
*ENDIF

! 2DSG
*IF, nSG, EQ, 2, then
	! Beam Model
	*if, Beam_flag, EQ, 1, then
		w= 1
	*endif
	
	! Plate/Shell Model
	*IF, Plate_flag, EQ, 1, then
		w= w2
	*endif
	
	! Solid Model
	*if, Solid_flag, eq, 1, then
		w= w2*w3
	*endif
*ENDIF

! 3DSG
*IF, nSG, EQ, 3, then	
	! Beam Model
	*if, Beam_flag, EQ, 1, then
		w= w1
	*endif
	
	! Plate/Shell Model
	*IF, Plate_flag, EQ, 1, then
		w= w1*w2
	*endif
	
	! Solid Model
	*if, Solid_flag, eq, 1, then
		w= w1*w2*w3
	*endif
*ENDIF
	
H_SCInput, mac

/GOPR