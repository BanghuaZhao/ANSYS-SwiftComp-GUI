!******************************************************
! Upload Input File for Laminate                      *
!                                                     *
! written by: Banghua Zhao                            *
! Date: 2015                                          *
!                                                     *
! Modified by: Banghua Zhao                           *
! Date: 2016                                          *
!******************************************************


/prep7 
/NOPR 

! Check Material Model
*Get, test, MAT, 0, COUNT
*IF, test, eq, 0, THEN
*MSG, ERROR
No Material Model found. %/&
Please create Material Model!
/EOF
*ENDIF

! Check keypoints
*GET, test, kp, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check lines
*GET, test, line, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check areas
*GET, test, area, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check volumes
*GET, test, volu, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check nodes
*GET, test, NODE, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check elements
*GET, test, ELEM, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

nSG=1
C1DSG_flag= 1
 
*DEL, name
*DEL, exten
*DEL, locat

*DIM, name, string, 200
*DIM, exten, string, 200
*DIM, locat, string, 200

*DEL, layupm
*DEL, layupa
*DEL, layupt           
*DEL, layermate_id
*DEL, angle
*DEL, e_layer_id

*DIM, layupm, ARRAY, 10000     ! Store layup material
*DIM, layupa, ARRAY, 10000     ! Store layup angle
*DIM, layupt, ARRAY, 10000     ! Store layup thickness
*DIM, layermate_id, ARRAY, 200, 1, 1
*DIM, angle,        ARRAY, 200, 1, 1
*DIM, e_layer_id, ARRAY, 10000, 1, 1

name(1)= ARG1
exten(1)= ARG2
locat(1)= ARG3
elementtype= ARG4
 
locat(Strleng(locat(1))+1)= name(1)

! How many segment
*Sread, layupnumber, locat(1), exten(1), , 64, , 1 
nsegment= Valchr(layupnumber(1))

nskip=1
layupn=0
sum=1

!++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
! Process data for each segment one by one
!++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*do, k, 1, nsegment

/UIS,MSGPOP,3 

! nlayermsn for each segment where m: # of layers, m: repeatition before symmetry, s: symmetry, n: repeatition after symmetry
*del, nlayermsn
*dim, nlayermsn, table, 1, 4,
*del, temparray
*dim, temparray, array, 1, 1
*tread, nlayermsn, locat(1), exten(1),  , nskip-1

*if, k, eq, 1, then
*vfun, temparray(1,1), copy, nlayermsn(0,1)
nlayertemp= temparray(1,1)
*vfun, temparray(1,1), copy, nlayermsn(0,2)
m= temparray(1,1)
*vfun, temparray(1,1), copy, nlayermsn(0,3)
s= temparray(1,1)
*vfun, temparray(1,1), copy, nlayermsn(0,4)
n= temparray(1,1)
*endif

*if, k, gt, 1, then
*vfun, temparray(1,1), copy, nlayermsn(0,3)
nlayertemp= temparray(1,1)
*vfun, temparray(1,1), copy, nlayermsn(0,4)
m= temparray(1,1)
*vfun, temparray(1,1), copy, nlayermsn(1,0)
s= temparray(1,1)
*vfun, temparray(1,1), copy, nlayermsn(1,1)
n= temparray(1,1)
*endif

! Check data
*if, nlayertemp, le, 0, then
*MSG,ERROR
Number of layer is less or equal to zero,%/&
Please recheck and carry on analysis!
/EOF
*endif

*if, m, le, 0, or, n, le, 0, then
*MSG,ERROR
Repeatition is less or equal to zero,%/&
Please recheck and carry on analysis!
/EOF
*endif

*if, s, lt, 0, or, s, gt, 1, then
*MSG,ERROR
Data of symmetry is wrong,%/&
Please recheck and carry on analysis!
/EOF
*endif

! temptable is responsible for reading data
*del, temptable
*dim, temptable, table, 1, 4, ,
*tread, temptable, locat(1), exten(1),  , nskip

*del, tam  ! tam is responsible for storing data tam: thinckness angle material#
*dim, tam, array, nlayertemp, 3, 1
*vfun, tam(1,1), copy, temptable(0,4)

*del, temptable
*dim, temptable, table, nlayertemp-1, 2, 
*tread, temptable, locat(1), exten(1),  , nskip+1

*vfun, tam(2,1), copy, temptable(1,0)
*vfun, tam(1,2), copy, temptable(0,1)
*vfun, tam(1,3), copy, temptable(0,2)

/UIS,MSGPOP,3 

*Get, nmate, MAT, 0, COUNT

*DEL, layupm_temp
*DEL, layupa_temp
*DEL, layupt_temp

*DIM, layupm_temp, ARRAY, 10000     ! Store layup material for segment
*DIM, layupa_temp, ARRAY, 10000     ! Store layup angle for segment
*DIM, layupt_temp, ARRAY, 10000     ! Store layup thickness for segment

*do, i, 1, nlayertemp

layupt_temp(i)=tam(i,1)

! Check thickness
*if, layupt_temp(i), le, 0, then
*MSG,ERROR
lamina thickness is less or equal to zero,%/&
Please recheck and carry on analysis!
/EOF
*ENDIF

layupa_temp(i)=tam(i,2)

layupm_temp(i)=tam(i,3)

! Check material #
*if, layupm_temp(i), le, 0, then
*MSG,ERROR
Material # is wrong,%/&
Please recheck and carry on analysis!
/EOF
*ENDIF

*if, layupm_temp(i), gt, nmate, then
*MSG,ERROR
Material # is greater than the # of Material Models,%/&
Please recheck and carry on analysis!
/EOF
*ENDIF

*enddo

nskip= nskip+nlayertemp+1

! Repetition before symmetry
*IF, m, GT, 1, THEN
	*DO, i, 1, m-1
		*DO, j, 1, nlayertemp
			layupt_temp(j+i*nlayertemp)= layupt_temp(j)
			layupa_temp(j+i*nlayertemp)= layupa_temp(j)  	
			layupm_temp(j+i*nlayertemp)= layupm_temp(j)
		*ENDDO
	*ENDDO
	nlayertemp= nlayertemp*m
*ENDIF

! Symmetry
*IF, s, eq, 1, THEN
	*DO, i, 1, nlayertemp  
		layupt_temp(i+nlayertemp)= layupt_temp(nlayertemp-(i-1))
		layupa_temp(i+nlayertemp)= layupa_temp(nlayertemp-(i-1))		
		layupm_temp(i+nlayertemp)= layupm_temp(nlayertemp-(i-1))
	*ENDDO
	nlayertemp= nlayertemp*2
*ENDIF

! Repetition after symmetry
*IF, n, GT, 1, THEN
	*DO, i, 1, n-1
		*DO, j, 1, nlayertemp 
			layupt_temp(j+i*nlayertemp)= layupt_temp(j)
			layupa_temp(j+i*nlayertemp)= layupa_temp(j) 			
			layupm_temp(j+i*nlayertemp)= layupm_temp(j)
		*ENDDO
	*ENDDO
	nlayertemp= nlayertemp*n
*ENDIF

*do, f, layupn+1, layupn+nlayertemp
layupt(f)=layupt_temp(f-layupn)
layupa(f)=layupa_temp(f-layupn)
layupm(f)=layupm_temp(f-layupn)
*enddo

layupn=layupn+nlayertemp

*enddo
!++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
! End of nsegment
!++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

sum=1
angle(1)= layupa(1)    
layermate_id(1)= layupm(1)
e_layer_id(1)= 1
*DO, i , 2, layupn
	k1= layupa(i)
	k2= layupm(i)
	p= 0
	*DO, j, 1, i-1
		*IF, k1, EQ, layupa(j), AND, k2, EQ, layupm(j), THEN
			p= j
			*EXIT
		*ENDIF
	*ENDDO
	*IF, p, EQ, 0, THEN
		sum=sum+1
		angle(sum)= k1
		layermate_id(sum)= k2
	*ENDIF
	*DO, j, 1, sum
		*IF, k1, EQ, angle(j), AND, k2, EQ, layermate_id(j), THEN
			e_layer_id(i)= j
			*EXIT
		*ENDIF
	*ENDDO
*ENDDO

nlayer= sum        ! Total # of layup type (total # of lamina type)

*DEL, layer_id
*DIM, layer_id, ARRAY, nlayer, 1, 1 
*DO, i , 1, nlayer
	layer_id(i)= i
*ENDDO


! elementtype=5
*if,elementtype, eq, 5, then
! Get node_x3 coordinate and plot the SG
*DEL, NODE_LST, ARRAY, 4*layupn+1
*DEL, NODE_X3, ARRAY, 4*layupn+1

*DIM, NODE_LST, ARRAY, 4*layupn+1
*DIM, NODE_X3, ARRAY, 4*layupn+1

*DO, i, 1, 4*layupn+1
	NODE_LST(i)= i
*ENDDO

NODE_X3(2*layupn+1)=0
*IF, mod(layupn,2), EQ, 0, Then
	t1=0
	t2=0
	*DO, i, 1, layupn/2
		t1= t1+ layupt(i)
		t2= t2+ layupt(i+layupn/2)
	*ENDDO
	NODE_X3(4*layupn+1)= t2
	sum=0
	*DO, i, 1, layupn/2
		*DO, j, 1, 4
			NODE_X3((i-1)*4+j)= sum+(j-1)*layupt(i)/4-t1
		*ENDDO
		sum= sum+ layupt(i)
	*ENDDO
	sum=0
	*DO, i, layupn/2+1, layupn
		*DO, j, 1, 4
			NODE_X3((i-1)*4+j)= sum+(j-1)*layupt(i)/4
		*ENDDO
		sum= sum+ layupt(i)
	*ENDDO
*ENDIF

*IF, mod(layupn,2), NE, 0, Then
	t1=0
	t2=0
	*DO, i, 1, (layupn+1)/2-1
		t1= t1+ layupt(i)
		t2= t2+ layupt(i+(layupn+1)/2)
	*ENDDO
	t1= t1+layupt((layupn+1)/2)/2
	t2= t2+layupt((layupn+1)/2)/2	
	NODE_X3(2*layupn+2)=layupt((layupn+1)/2)/4
	NODE_X3(2*layupn+1)=0
	NODE_X3(2*layupn)=-layupt((layupn+1)/2)/4
	NODE_X3(2*layupn-1)=-2*layupt((layupn+1)/2)/4
	sum=0
	*DO, i, 1, (layupn+1)/2-1
		*DO, j, 1, 4
			NODE_X3((i-1)*4+j)= sum+(j-1)*layupt(i)/4-t1
		*ENDDO
		sum= sum+ layupt(i)
	*ENDDO
	sum=layupt((layupn+1)/2)/2	
	*DO, i, (layupn+1)/2+1, layupn
		*DO, j, 1, 4
			NODE_X3((i-1)*4+j)= sum+(j-1)*layupt(i)/4
		*ENDDO
		sum= sum+ layupt(i)
	*ENDDO
	NODE_X3(4*layupn+1)= t2
*ENDIF

*DO, i, 1, 4*layupn+1
	k, i, 0, NODE_X3(i), 0
*ENDDO

*DO, i, 1, 4*layupn
	L, i, i+1
*ENDDO
*endif

! elementtype=2
*if, elementtype, eq, 2, then
*IF, mod(layupn,2), ne, 0, THEN
*MSG,ERROR
The number of layers is not even,%/&
Please input even number of layers for 2 node element!
/EOF
*ENDIF

! Get node_x3 coordinate and plot the SG
*DEL, NODE_LST, ARRAY, layupn+1
*DEL, NODE_X3, ARRAY, layupn+1

*DIM, NODE_LST, ARRAY, layupn+1
*DIM, NODE_X3, ARRAY, layupn+1

*DO, i, 1, layupn+1
	NODE_LST(i)= i
*ENDDO

NODE_X3(layupn/2+1)=0
t1=0
t2=0
*DO, i, 1, layupn/2
	t1= t1+ layupt(i)
	t2= t2+ layupt(i+layupn/2)
*ENDDO
NODE_X3(layupn+1)= t2
sum=0
*DO, i, 1, layupn
	NODE_X3(i)= -t1+sum
	sum= sum+ layupt(i)
*ENDDO

*DO, i, 1, layupn+1
	k, i, 0, NODE_X3(i), 0
*ENDDO

*DO, i, 1, layupn
	L, i, i+1
*ENDDO
*endif

C1DSG_thickness=0
*do, i, 1, layupn
	C1DSG_thickness= C1DSG_thickness+ layupt(i)
*enddo

/Title, 1D SG, Number of layers=%layupn%

local, 100, 0, 0, 0, 0, -90, 0, -90
DSYS,100, 
/VIEW,1,1,0,0   
/VUP,1,Z

GPLOT 

/GOPR