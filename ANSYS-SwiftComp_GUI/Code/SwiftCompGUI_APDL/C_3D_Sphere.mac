!******************************************************
! Create Spherical Inclusions Microstructure          *
!                                                     *
! Written by: Yezi                                    *
! Date: SEP 2009                                      *
!                                                     *
! Modified by: Banghua Zhao                           *
! Date: 2016                                          *
!******************************************************


/prep7
/NOPR  

! Check Material Model
*Get, test, MAT, 0, COUNT
*IF, test, eq, 0, THEN
*MSG, ERROR
No Material Model found. %/&
Please create Material Model!
/EOF
*ENDIF

! Check keypoints
*GET, test, kp, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check lines
*GET, test, line, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check areas
*GET, test, area, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check volumes
*GET, test, volu, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check nodes
*GET, test, NODE, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

! Check elements
*GET, test, ELEM, 0, COUNT
*IF, test, ne, 0, THEN
*MSG, ERROR
Please creat a new file to carry on analysis!
/EOF
*ENDIF

nSG = 3
L = 1
Vf = ARG1
Material1 = ARG2
Material2 = ARG3
elemtype = ARG4
meshtype = 1   !0: Free mesh    1: mapped mesh
meshsize= ARG5*10
helpelement= 'shell93'

*IF, Vf, GT, 0.523, OR, Vf, Le, 0, THEN
*MSG,ERROR
Volume fraction of sphere is wrong,%/&
Please recheck and carry on analysis!
/EOF
*ENDIF

*IF, Vf, GT, 0.5, then
*MSG,ERROR
Volume fraction of sphere is too large,%/&
Suggest: 0.01<VOS<0.5
/EOF
*ENDIF

*IF, Vf, lt, 0.01, then
*MSG,ERROR
Volume fraction of sphere is too small,%/&
Suggest: 0.01<VOS<0.5
/EOF
*ENDIF

! Check Material #
*if, material1, le, 0, or, material2, le, 0, then
*MSG,ERROR
Material # for fiber or matrix is wrong,%/&
Please recheck and carry on analysis!
/EOF
*ENDIF

*Get, nmate, MAT, 0, COUNT

*if, material1, gt, nmate, then
*MSG,ERROR
Material # is greater than the # of Material Models,%/&
Please recheck and carry on analysis!
/EOF
*ENDIF

*if, material2, gt, nmate, then
*MSG,ERROR
Material # is greater than the # of Material Models,%/&
Please recheck and carry on analysis!
/EOF
*ENDIF

*If, elemtype, eq, 285, then
	elementtype= 'SOLID285'
*ENDIF
*If, elemtype, eq, 187, then
	elementtype= 'SOLID187'
*ENDIF
*If, elemtype, eq, 185, then
	elementtype= 'SOLID185'
*ENDIF
*If, elemtype, eq, 186, then
	elementtype= 'SOLID186'
*ENDIF


*IF, elementtype, EQ, 'SOLID92', THEN
	meshtype = 0
*ENDIF
*IF, elementtype, EQ, 'SOLID187', THEN
	meshtype = 0
*ENDIF
*IF, elementtype, EQ, 'SOLID285', THEN
	meshtype = 0
*ENDIF


! Create geometry
Pi=3.14159265358979323846264338327950288419716939937510
r = ((3*Vf*L*L*L)/(4*Pi))**(1/3)
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!     8-noded brick elements: SOLID45 or SOLID185
!     20-noded brick elements: SOLID95 or SOLID186
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*IF, meshtype, EQ, 1, THEN
	blc4, 0, 0, L/2, L/2, L/2
	SPHERE, r, 0, 0 ,90

	VOVLAP, ALL
	VDELE, 4, , ,1

	asel, , , , 2, 6, 2
	accat,all
	
	lccat,8,12
	lccat,5,10
	lccat,2,3
	
	NUMMRG, ALL
	NUMCMP, ALL	
	
	! Meshing 
	
	ET, 1, elementtype
		
	elementshape = 0      ! elementtype =1 -> tetrahedral element shape 
	
    !     4-noded tetrahedral elements: SOLID285 
    !     10-noded tetrahedral elements: SOLID92 or SOLID187 
    !     8-noded brick elements: SOLID45 or SOLID185
    !     20-noded brick elements: SOLID95 or SOLID186

	
	
	MSHAPE, elementshape, 3D 
	MSHKEY, meshtype

	ESIZE, L/meshsize, ,
	
	MAT, Material1
	VMESH, 1
	 
	MAT, Material2
	VMESH, 2
	
	/UIS,MSGPOP,3 
	VSYMM, X, ALL
	VSYMM, Y, ALL
	VSYMM, Z, ALL
	/UIS,MSGPOP,2
	
	NUMMRG, ALL
	NUMCMP, ALL
	
	EPLOT
	/VIEW,1,1,1,1   
	/ANG,1  
	/REP,FAST  
*ENDIF
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!     4-noded tetrahedral elements: SOLID285 
!     10-noded tetrahedral elements: SOLID92 or SOLID187 
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*IF, meshtype, EQ, 0, THEN

	BLOCK, -L/2, L/2, -L/2, L/2, -L/2, L/2 
	SPHERE,r, ,0,360, 

	VOVLAP, ALL

	NUMMRG, ALL
	NUMCMP, ALL	
	
	VGLUE, ALL
	
	NUMMRG, ALL
	NUMCMP, ALL	
	
	! Meshing
	
	ET, 1, elementtype
	et, 2, helpelement
	
	elementshape = 1      ! elementtype =1 -> tetrahedral element shape 
	
	!        Solid 92 for 10-noded tetrahedral elements

	/UIS, MSGPOP,3
		
	MSHKEY, meshtype    !Free Meshing
	
	ESIZE, L/meshsize, ,
	
	MAT, Material1
	VMESH, 1


	MAT,Material2
	
	MSHKEY,1
	AMESH,1,6,1
	
	MSHKEY,0
	VMESH,2
	
	ACLEAR,1,6,1
	
	ETDEL,2 
	
	NUMMRG, ALL
	NUMCMP, ALL
	
	/UIS, MSGPOP,1
	
	EPLOT
	
	/VIEW,1,1,1,1   
	/ANG,1  
	/REP,FAST
	
*ENDIF
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


local, 100, 0, 0, 0, 0, -90, 0, -90
DSYS,100, 

EPLOT
/NUMBER,1   
/PNUM,MAT,1 
/REPLOT
	
/GOPR	